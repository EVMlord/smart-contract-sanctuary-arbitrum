// SPDX-License-Identifier: MIT

// solhint-disable-next-line compiler-version
pragma solidity ^0.8.0;

import "../../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {UpgradeableProxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 */
abstract contract Initializable {

    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        require(_initializing || !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain`call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: value }(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.staticcall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;
import "../proxy/utils/Initializable.sol";

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal initializer {
        __Context_init_unchained();
    }

    function __Context_init_unchained() internal initializer {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;

abstract contract BlockContext {
    function _blockTimestamp() internal view virtual returns (uint256) {
        // Reply from Arbitrum
        // block.timestamp returns timestamp at the time at which the sequencer receives the tx.
        // It may not actually correspond to a particular L1 block
        return block.timestamp;
    }

    function _blockNumber() internal view virtual returns (uint256) {
        return block.number;
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;

import { ContextUpgradeable } from "@openzeppelin/contracts-upgradeable/utils/ContextUpgradeable.sol";
import { Constant } from "../lib/Constant.sol";

abstract contract SafeOwnable is ContextUpgradeable {
    address private _owner;
    address private _candidate;

    // __gap is reserved storage
    uint256[50] private __gap;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        // SO_CNO: caller not owner
        require(owner() == _msgSender(), "SO_CNO");
        _;
    }

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */

    function __SafeOwnable_init() internal initializer {
        __Context_init();
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(Constant.ADDRESS_ZERO, msgSender);
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() external virtual onlyOwner {
        // emitting event first to avoid caching values
        emit OwnershipTransferred(_owner, Constant.ADDRESS_ZERO);
        _owner = Constant.ADDRESS_ZERO;
        _candidate = Constant.ADDRESS_ZERO;
    }

    /**
     * @dev Set ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function setOwner(address newOwner) external onlyOwner {
        // SO_NW0: newOwner is 0
        require(newOwner != Constant.ADDRESS_ZERO, "SO_NW0");
        // SO_SAO: same as original
        require(newOwner != _owner, "SO_SAO");
        // SO_SAC: same as candidate
        require(newOwner != _candidate, "SO_SAC");

        _candidate = newOwner;
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`_candidate`).
     * Can only be called by the new owner.
     */
    function updateOwner() external {
        // SO_C0: candidate is zero
        require(_candidate != Constant.ADDRESS_ZERO, "SO_C0");
        // SO_CNC: caller is not candidate
        require(_candidate == _msgSender(), "SO_CNC");

        // emitting event first to avoid caching values
        emit OwnershipTransferred(_owner, _candidate);
        _owner = _candidate;
        _candidate = Constant.ADDRESS_ZERO;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Returns the candidate that can become the owner.
     */
    function candidate() external view returns (address) {
        return _candidate;
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;
pragma abicoder v2;

import { SafeOwnable } from "../base/SafeOwnable.sol";

abstract contract ClearingHouseCallee is SafeOwnable {
    address internal _clearingHouse;
    uint256[50] private __gap;

    event ClearingHouseChanged(address indexed clearingHouse);

    // solhint-disable-next-line func-order
    function __ClearingHouseCallee_init() internal initializer {
        __SafeOwnable_init();
    }

    function setClearingHouse(address clearingHouseArg) external onlyOwner {
        _clearingHouse = clearingHouseArg;
        emit ClearingHouseChanged(clearingHouseArg);
    }

    function getClearingHouse() external view returns (address) {
        return _clearingHouse;
    }

    function _requireOnlyClearingHouse() internal view {
        // CHD_OCH: only ClearingHouse
        require(_msgSender() == _clearingHouse, "CHD_OCH");
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;
pragma abicoder v2;

import { FullMath } from "../lib/FullMath.sol";
import { FairtradeMath } from "../lib/FairtradeMath.sol";
import { Funding } from "../lib/Funding.sol";
import { Constant } from "../lib/Constant.sol";
import { IAmmFactory } from "../interface/IAmmFactory.sol";
import { ILiquidityProvider } from "../interface/ILiquidityProvider.sol";
import { IAmm } from "../interface/IAmm.sol";
import { BlockContext } from "../base/BlockContext.sol";
import { LiquidityProviderStorageV1, Account } from "../storage/LiquidityProviderStorage.sol";
import { ClearingHouseCallee } from "./ClearingHouseCallee.sol";

// never inherit any new stateful contract. never change the orders of parent stateful contracts
contract LiquidityProvider is ILiquidityProvider, BlockContext, ClearingHouseCallee, LiquidityProviderStorageV1 {
    using FairtradeMath for uint256;
    using FairtradeMath for int256;

    //
    // STRUCT
    //

    struct InternalAddLiquidityParams {
        address maker;
        address baseToken;
        address amm;
        uint256 liquidity;
        uint256 base;
        uint256 quote;
        Funding.Growth globalFundingGrowth;
    }

    struct InternalRemoveLiquidityParams {
        address maker;
        address baseToken;
        address amm;
        uint256 liquidity;
        uint256 minBase;
        uint256 minQuote;
    }

    //
    // EXTERNAL NON-VIEW
    //

    function initialize(address ammFactoryArg) external initializer {
        __ClearingHouseCallee_init();

        _ammFactory = ammFactoryArg;
    }

    function setMarketTaker(address marketTakerArg) external onlyOwner {
        _marketTaker = marketTakerArg;
        emit MarketTakerChanged(marketTakerArg);
    }

    function addLiquidity(AddLiquidityParams calldata params) external override returns (AddLiquidityResponse memory) {
        _requireOnlyClearingHouse();

        address amm = IAmmFactory(_ammFactory).getAmm(params.baseToken);

        // add liquidity to amm
        (uint256 quoteAmount, uint256 baseAmount, uint256 liquidity) = IAmm(amm).addLiquidity(
            params.quote,
            params.base,
            params.minLiquidity
        );

        // state changes; if adding liquidity to an existing order, get fees accrued
        uint256 fee = _addLiquidityToOrder(
            InternalAddLiquidityParams({
                maker: params.maker,
                baseToken: params.baseToken,
                amm: amm,
                liquidity: liquidity,
                base: baseAmount,
                quote: quoteAmount,
                globalFundingGrowth: params.fundingGrowthGlobal
            })
        );

        return AddLiquidityResponse({ base: baseAmount, quote: quoteAmount, fee: fee, liquidity: liquidity });
    }

    function removeLiquidity(RemoveLiquidityParams calldata params)
        external
        override
        returns (RemoveLiquidityResponse memory)
    {
        _requireOnlyClearingHouse();

        Account.LPInfo storage openOrder = _openOrderMap[params.maker][params.baseToken];
        // LP_LOF: remove too much liquidity
        require(params.liquidity <= openOrder.liquidity, "LP_RTML");

        address amm = IAmmFactory(_ammFactory).getAmm(params.baseToken);
        return
            _removeLiquidity(
                InternalRemoveLiquidityParams({
                    maker: params.maker,
                    baseToken: params.baseToken,
                    amm: amm,
                    liquidity: params.liquidity,
                    minBase: params.minBase,
                    minQuote: params.minQuote
                })
            );
    }

    /// @inheritdoc ILiquidityProvider
    function updateFundingGrowthAndLiquidityFundingPayment(
        address trader,
        address baseToken,
        Funding.Growth memory fundingGrowthGlobal
    ) external override returns (int256 liquidityFundingPayment) {
        // LP_OMT: Only MarketTaker
        require(_msgSender() == _marketTaker, "LP_OMT");

        Account.LPInfo storage order = _openOrderMap[trader][baseToken];

        // funding of liquidity
        liquidityFundingPayment = Funding.calcLiquidityFundingPayment(order, fundingGrowthGlobal);

        // thus, state updates have to come after
        order.lastTwPremiumGrowth = fundingGrowthGlobal.twPremium;
        order.lastTwPremiumWithLiquidityGrowth = fundingGrowthGlobal.twPremiumWithLiquidity;

        return liquidityFundingPayment;
    }

    // function updateLPOrderDebt(
    //     address trader,
    //     address baseToken,
    //     int256 base,
    //     int256 quote
    // ) external override {
    //     _requireOnlyClearingHouse();
    //     Account.LPInfo storage openOrder = _openOrderMap[trader][baseToken];
    //     openOrder.baseDebt = (openOrder.baseDebt.toInt256() + base).toUint256();
    //     openOrder.quoteDebt = (openOrder.quoteDebt.toInt256() + quote).toUint256();
    // }

    //
    // EXTERNAL VIEW
    //

    /// @inheritdoc ILiquidityProvider
    function getMarketTaker() external view override returns (address) {
        return _marketTaker;
    }

    function getOpenOrder(address trader, address baseToken) external view override returns (Account.LPInfo memory) {
        return _openOrderMap[trader][baseToken];
    }

    function hasOrder(address trader, address[] calldata tokens) external view override returns (bool) {
        for (uint256 i = 0; i < tokens.length; i++) {
            if (_openOrderMap[trader][tokens[i]].liquidity != 0) {
                return true;
            }
        }
        return false;
    }

    function getTotalLPQuoteAmountAndPendingFee(address trader, address[] calldata baseTokens)
        external
        view
        override
        returns (int256 totalQuoteAmountInAmms, uint256 totalPendingFee)
    {
        for (uint256 i = 0; i < baseTokens.length; i++) {
            address baseToken = baseTokens[i];
            (int256 lpQuoteAmount, uint256 pendingFee) = _getLPQuoteAmountAndPendingFee(trader, baseToken, false);
            totalQuoteAmountInAmms = totalQuoteAmountInAmms + lpQuoteAmount;
            totalPendingFee = totalPendingFee + pendingFee;
        }
        return (totalQuoteAmountInAmms, totalPendingFee);
    }

    /// @inheritdoc ILiquidityProvider
    function getTotalTokenAmountInAmmAndPendingFee(
        address trader,
        address baseToken,
        bool fetchBase // true: fetch base amount, false: fetch quote amount
    ) external view override returns (uint256 tokenAmount, uint256 pendingFee) {
        (tokenAmount, pendingFee) = _getTotalTokenAmountInAmm(trader, baseToken, fetchBase);
    }

    /// @inheritdoc ILiquidityProvider
    function getLiquidityFundingPayment(
        address trader,
        address baseToken,
        Funding.Growth memory fundingGrowthGlobal
    ) external view override returns (int256 liquidityFundingPayment) {
        Account.LPInfo memory order = _openOrderMap[trader][baseToken];

        // funding of liquidity
        liquidityFundingPayment = Funding.calcLiquidityFundingPayment(order, fundingGrowthGlobal);

        return liquidityFundingPayment;
    }

    function getPendingFee(address trader, address baseToken) external view override returns (uint256) {
        uint256 pendingFee = _getPendingFee(baseToken, _openOrderMap[trader][baseToken]);
        return pendingFee;
    }

    //
    // PUBLIC VIEW
    //

    function getTotalLPOrderDebt(
        address trader,
        address baseToken,
        bool fetchBase
    ) public view override returns (uint256) {
        Account.LPInfo memory orderInfo = _openOrderMap[trader][baseToken];
        uint256 totalLPOrderDebt = fetchBase ? orderInfo.baseDebt : orderInfo.quoteDebt;

        return totalLPOrderDebt;
    }

    //
    // INTERNAL NON-VIEW
    //

    function _removeLiquidity(InternalRemoveLiquidityParams memory params)
        internal
        returns (RemoveLiquidityResponse memory)
    {
        (uint256 quote, uint256 base) = IAmm(params.amm).removeLiquidity(
            params.liquidity,
            params.minQuote,
            params.minBase
        );

        // update token info based on existing open order
        (uint256 fee, uint256 baseDebt, uint256 quoteDebt) = _removeLiquidityFromOrder(params);

        int256 takerBase = base.toInt256() - baseDebt.toInt256();
        int256 takerQuote = quote.toInt256() - quoteDebt.toInt256();

        return
            RemoveLiquidityResponse({
                base: base,
                quote: quote,
                fee: fee,
                takerBase: takerBase,
                takerQuote: takerQuote
            });
    }

    function _removeLiquidityFromOrder(InternalRemoveLiquidityParams memory params)
        internal
        returns (
            uint256 fee,
            uint256 baseDebt,
            uint256 quoteDebt
        )
    {
        // update token info based on existing open order
        Account.LPInfo storage openOrder = _openOrderMap[params.maker][params.baseToken];

        // as in _addLiquidityToOrder(), fee should be calculated before the states are updated
        fee = _getPendingFee(params.baseToken, openOrder);

        if (params.liquidity != 0) {
            if (openOrder.baseDebt != 0) {
                baseDebt = FullMath.mulDiv(openOrder.baseDebt, params.liquidity, openOrder.liquidity);
                openOrder.baseDebt = openOrder.baseDebt - baseDebt;
            }
            if (openOrder.quoteDebt != 0) {
                quoteDebt = FullMath.mulDiv(openOrder.quoteDebt, params.liquidity, openOrder.liquidity);
                openOrder.quoteDebt = openOrder.quoteDebt - quoteDebt;
            }
            openOrder.liquidity = openOrder.liquidity - params.liquidity;
        }

        // after the fee is calculated, lastExchangeFeeIndex can be updated if liquidity != 0 after removing
        if (openOrder.liquidity == 0) {
            _removeOrder(params.maker, params.baseToken);
        } else {
            openOrder.lastExchangeFeeIndex = _feeGrowthPerLiquidityUnitIndexMap[params.baseToken];
        }

        return (fee, baseDebt, quoteDebt);
    }

    function _removeOrder(address maker, address baseToken) internal {
        delete _openOrderMap[maker][baseToken];
    }

    /// @dev this function is extracted from and only used by addLiquidity() to avoid stack too deep error
    function _addLiquidityToOrder(InternalAddLiquidityParams memory params) internal returns (uint256) {
        // get the struct by key, no matter it's a new or existing order
        Account.LPInfo storage openOrder = _openOrderMap[params.maker][params.baseToken];

        // initialization for a new order
        if (openOrder.liquidity == 0) {
            openOrder.lastTwPremiumGrowth = params.globalFundingGrowth.twPremium;
            openOrder.lastTwPremiumWithLiquidityGrowth = params.globalFundingGrowth.twPremiumWithLiquidity;
        }

        // fee should be calculated before the states are updated, as for
        // - a new order, there is no fee accrued yet
        // - an existing order, fees accrued have to be settled before more liquidity is added
        uint256 fee = _getPendingFee(params.baseToken, openOrder);

        // after the fee is calculated, liquidity & lastExchangeFeeIndex can be updated
        openOrder.liquidity = openOrder.liquidity + params.liquidity;
        openOrder.lastExchangeFeeIndex = _feeGrowthPerLiquidityUnitIndexMap[params.baseToken];
        openOrder.baseDebt = openOrder.baseDebt + params.base;
        openOrder.quoteDebt = openOrder.quoteDebt + params.quote;

        return fee;
    }

    //
    // INTERNAL VIEW
    //

    /// @return lpQuoteAmount includes maker fee
    function _getLPQuoteAmountAndPendingFee(
        address trader,
        address baseToken,
        bool fetchBase
    ) internal view returns (int256, uint256) {
        (uint256 totalAmountFromOrders, uint256 pendingFee) = _getTotalTokenAmountInAmm(trader, baseToken, fetchBase);
        uint256 totalLPOrderDebt = getTotalLPOrderDebt(trader, baseToken, fetchBase);

        // lpAmount = totalTokenAmountInAmm - totalLPOrderDebt
        return (totalAmountFromOrders.toInt256() - totalLPOrderDebt.toInt256(), pendingFee);
    }

    /// @dev Get total amount of the specified tokens in the specified amm.
    ///      Note:
    ///        1. when querying quote amount, it includes exchange fees, i.e.:
    ///           quote amount = quote liquidity + fees
    ///           base amount = base liquidity
    ///        2. quote/base liquidity does NOT include amm fees since
    ///           they do not have any impact to our margin system
    ///        3. the returned fee amount is only meaningful when querying quote amount
    function _getTotalTokenAmountInAmm(
        address trader,
        address baseToken, // this argument is only for specifying which amm to get base or quote amounts
        bool fetchBase // true: fetch base amount, false: fetch quote amount
    ) internal view returns (uint256 tokenAmount, uint256 pendingFee) {
        Account.LPInfo memory order = _openOrderMap[trader][baseToken];
        address amm = IAmmFactory(_ammFactory).getAmm(baseToken);

        if (order.liquidity == 0) {
            return (0, 0);
        }

        (uint256 quoteAmount, uint256 baseAmount) = IAmm(amm).calcTokenAmountsByLiquidity(order.liquidity);
        tokenAmount = fetchBase ? baseAmount : quoteAmount;
        // get uncollected fee (only quote)
        if (!fetchBase) {
            uint256 pendingFeeInOrder = _getPendingFee(baseToken, order);
            pendingFee = pendingFee + pendingFeeInOrder;
        }

        return (tokenAmount, pendingFee);
    }

    function _getPendingFee(address baseToken, Account.LPInfo memory order) internal view returns (uint256) {
        if (order.liquidity == 0 || _feeGrowthPerLiquidityUnitIndexMap[baseToken] == 0) {
            return 0;
        }

        uint256 lpFeeRatioAccumulate;
        if (order.lastExchangeFeeIndex < _feeGrowthPerLiquidityUnitIndexMap[baseToken] - 1) {
            uint256 latestValue = _feeGrowthPerLiquidityUnitMap[baseToken][
                _feeGrowthPerLiquidityUnitIndexMap[baseToken] - 1
            ];
            uint256 startValue = _feeGrowthPerLiquidityUnitMap[baseToken][order.lastExchangeFeeIndex];
            lpFeeRatioAccumulate = latestValue - startValue;
        }

        // use Q96 precision to avoid in calculation result error
        return (order.liquidity * lpFeeRatioAccumulate) / Constant.IQ96;
    }

    function updateLPCalcFeeRatio(address baseToken, uint256 lpCalcFeeRatio) external override {
        // LP_OMT: only MarketTaker
        require(_msgSender() == _marketTaker, "LP_OMT");

        uint256 previousValue = 0;
        if (_feeGrowthPerLiquidityUnitIndexMap[baseToken] > 0) {
            previousValue = _feeGrowthPerLiquidityUnitMap[baseToken][_feeGrowthPerLiquidityUnitIndexMap[baseToken] - 1];
        }
        _feeGrowthPerLiquidityUnitMap[baseToken][_feeGrowthPerLiquidityUnitIndexMap[baseToken]] =
            previousValue +
            lpCalcFeeRatio;
        _feeGrowthPerLiquidityUnitIndexMap[baseToken]++;
    }

    function getLPCalcFeeRatio(address baseToken, uint256 index) external view returns (uint256) {
        return _feeGrowthPerLiquidityUnitMap[baseToken][index];
    }

    function getLPCalcFeeRatioIndex(address baseToken) external view returns (uint256) {
        return _feeGrowthPerLiquidityUnitIndexMap[baseToken];
    }

    function setLPCalcFeeRatio(
        address baseToken,
        uint256 startIndex,
        uint256 endIndex
    ) external {
        for (uint256 i = startIndex; i < endIndex; i++) {
            _feeGrowthPerLiquidityUnitMap[baseToken][i] =
                _feeGrowthPerLiquidityUnitMap[baseToken][i > 0 ? i - 1 : 0] +
                _totalLPCalcFeeRatioMap[baseToken][i];
            _feeGrowthPerLiquidityUnitIndexMap[baseToken]++;
        }
    }

    function getLPCalcFeeRatioLength(address baseToken) external view returns (uint256) {
        return _totalLPCalcFeeRatioMap[baseToken].length;
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;

interface IAmm {
    /// @param coinPairs 0: quote token address, 1: base token address
    struct InitializeParams {
        uint256 A;
        uint256 gamma;
        uint256 adjustmentStep;
        uint256 maHalfTime;
        uint256 initialPrice;
        address baseToken;
        address quoteToken;
        address clearingHouse;
        address marketTaker;
        address liquidityProvider;
    }

    // Events
    event TokenExchange(address indexed buyer, uint256 i, uint256 dx, uint256 j, uint256 dy, bool isSell);

    event AddLiquidity(
        address indexed provider,
        uint256 amount0,
        uint256 amount1,
        uint256 liquidity,
        uint256 totalLiquidity
    );

    event RemoveLiquidity(
        address indexed provider,
        uint256 amount0,
        uint256 amount1,
        uint256 liquidity,
        uint256 totalLiquidity
    );

    event CommitNewParameters(uint256 indexed deadline, uint256 adjustmentStep, uint256 maHalfTime);

    event NewParameters(uint256 adjustmentStep, uint256 maHalfTime);

    event RampAgamma(
        uint256 initialA,
        uint256 futureA,
        uint256 initialGamma,
        uint256 futureGamma,
        uint256 initialTime,
        uint256 futureTime
    );

    event Repegging(
        address sender,
        uint256 accumulatedFees,
        uint256 loss,
        uint256 newD,
        uint256 priceScale,
        uint256 priceOracle
    );

    event StopRampA(uint256 currentA, uint256 currentGamma, uint256 time);

    event CalcPriceAfterSwap(address sender, uint256 amountIn, uint256 amountOut, uint256 priceAfter, bool isSell);

    event ClearingHouseChanged(address clearingHouse);

    event MarketTakerChanged(address marketTaker);

    event LiquidityProviderChanged(address liquidityProvider);

    event TotalValueLockedCapChanged(uint256 cap);

    function addLiquidity(
        uint256 amount0Desired,
        uint256 amount1Desired,
        uint256 minLiquidity
    )
        external
        returns (
            uint256,
            uint256,
            uint256
        );

    function removeLiquidity(
        uint256 liquidity,
        uint256 minAmount0,
        uint256 minAmount1
    ) external returns (uint256 amount0, uint256 amount1);

    function swap(
        uint256 i,
        uint256 j,
        uint256 dx,
        uint256 dyLimit,
        bool isSell
    ) external returns (uint256);

    function simulatedSwap(
        uint256 i,
        uint256 j,
        uint256 dx,
        bool isSell
    )
        external
        returns (
            uint256,
            uint256,
            uint256
        );

    function getDy(
        uint256 i,
        uint256 j,
        uint256 dx,
        bool isSell
    ) external view returns (uint256);

    function getA() external view returns (uint256);

    function getGamma() external view returns (uint256);

    function getCoins(uint256 i) external view returns (address);

    function getBalances(uint256 i) external view returns (uint256);

    function getPriceScale() external view returns (uint256);

    function getPriceOracle() external view returns (uint256);

    function getPriceLast() external view returns (uint256);

    function getPriceCurrent() external view returns (uint256);

    function getTwapMarkPrice(uint256 interval) external view returns (uint256);

    function getTotalLiquidity() external view returns (uint256);

    function getTotalVauleLockedCap() external view returns (uint256);

    function calcTokenAmountsByLiquidity(uint256 liquidity) external view returns (uint256 amount0, uint256 amount1);

    function calcLiquidityByTokenAmounts(uint256 amount0Desired, uint256 amount1Desired)
        external
        view
        returns (uint256);
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;
pragma abicoder v2;

interface IAmmFactory {
    struct AmmInfo {
        address amm;
        uint24 exchangeFeeRatio;
        uint24 insuranceFundFeeRatio;
    }

    event AmmAdded(address indexed baseToken, uint24 indexed exchangeFeeRatio, address indexed amm);

    event ExchangeFeeRatioChanged(address baseToken, uint24 exchangeFeeRatio);

    event InsuranceFundFeeRatioChanged(uint24 insuranceFundFeeRatio);

    function addAmm(
        address baseToken,
        address amm,
        uint24 exchangeFeeRatio
    ) external;

    function setExchangeFeeRatio(address baseToken, uint24 exchangeFeeRatio) external;

    function setInsuranceFundFeeRatio(address baseToken, uint24 insuranceFundFeeRatioArg) external;

    function getAmm(address baseToken) external view returns (address);

    function getExchangeFeeRatio(address baseToken) external view returns (uint24);

    function getInsuranceFundFeeRatio(address baseToken) external view returns (uint24);

    function getAmmInfo(address baseToken) external view returns (AmmInfo memory);

    function getQuoteToken() external view returns (address);

    function hasAmm(address baseToken) external view returns (bool);
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;
pragma abicoder v2;

import { Funding } from "../lib/Funding.sol";
import { Account } from "../lib/Account.sol";

interface ILiquidityProvider {
    struct AddLiquidityParams {
        address maker;
        address baseToken;
        uint256 base;
        uint256 quote;
        uint256 minLiquidity;
        Funding.Growth fundingGrowthGlobal;
    }

    struct RemoveLiquidityParams {
        address maker;
        address baseToken;
        uint256 liquidity;
        uint256 minBase;
        uint256 minQuote;
    }

    struct AddLiquidityResponse {
        uint256 base;
        uint256 quote;
        uint256 fee;
        uint256 liquidity;
    }

    struct RemoveLiquidityResponse {
        uint256 base;
        uint256 quote;
        uint256 fee;
        int256 takerBase;
        int256 takerQuote;
    }

    /// @param trader the address of trader contract
    event MarketTakerChanged(address indexed trader);

    function addLiquidity(AddLiquidityParams calldata params) external returns (AddLiquidityResponse memory);

    function removeLiquidity(RemoveLiquidityParams calldata params) external returns (RemoveLiquidityResponse memory);

    // function updateLPOrderDebt(
    //     address trader,
    //     address baseToken,
    //     int256 base,
    //     int256 quote
    // ) external;

    function getOpenOrder(address trader, address baseToken) external view returns (Account.LPInfo memory);

    function hasOrder(address trader, address[] calldata tokens) external view returns (bool);

    function getTotalLPQuoteAmountAndPendingFee(address trader, address[] calldata baseTokens)
        external
        view
        returns (int256 totalQuoteAmountInAmms, uint256 totalPendingFee);

    /// @dev the returned quote amount does not include funding payment because
    ///      the latter is counted directly toward realizedPnl.
    ///      the return value includes maker fee.
    ///      please refer to _getTotalTokenAmountInAmm() docstring for specs
    function getTotalTokenAmountInAmmAndPendingFee(
        address trader,
        address baseToken,
        bool fetchBase
    ) external view returns (uint256 tokenAmount, uint256 totalPendingFee);

    function getTotalLPOrderDebt(
        address trader,
        address baseToken,
        bool fetchBase
    ) external view returns (uint256);

    /// @dev this is the view version of updateFundingGrowthAndLiquidityFundingPayment()
    /// @return liquidityFundingPayment the funding payment of all orders/liquidity of a maker
    function getLiquidityFundingPayment(
        address trader,
        address baseToken,
        Funding.Growth memory fundingGrowthGlobal
    ) external view returns (int256 liquidityFundingPayment);

    function getPendingFee(address trader, address baseToken) external view returns (uint256);

    /// @dev this is the non-view version of getLiquidityFundingPayment()
    /// @return liquidityFundingPayment the funding payment of all orders/liquidity of a maker
    function updateFundingGrowthAndLiquidityFundingPayment(
        address trader,
        address baseToken,
        Funding.Growth memory fundingGrowthGlobal
    ) external returns (int256 liquidityFundingPayment);

    function getMarketTaker() external view returns (address);

    function updateLPCalcFeeRatio(address baseToken, uint256 fee) external;
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;

library Account {
    struct TakerInfo {
        int256 takerPositionSize;
        int256 takerOpenNotional;
        int256 lastTwPremiumGrowthGlobal;
    }

    struct LPInfo {
        uint256 liquidity;
        uint256 lastExchangeFeeIndex;
        int256 lastTwPremiumGrowth;
        int256 lastTwPremiumWithLiquidityGrowth;
        uint256 baseDebt;
        uint256 quoteDebt;
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;

library Constant {
    address internal constant ADDRESS_ZERO = address(0);
    uint256 internal constant DECIMAL_ONE = 1e18;
    int256 internal constant DECIMAL_ONE_SIGNED = 1e18;
    uint256 internal constant IQ96 = 0x1000000000000000000000000;
    int256 internal constant IQ96_SIGNED = 0x1000000000000000000000000;
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;
pragma abicoder v2;

import { FullMath } from "./FullMath.sol";
import { Constant } from "./Constant.sol";

library FairtradeMath {
    function copy(uint256[2] memory data) internal pure returns (uint256[2] memory) {
        uint256[2] memory result;
        for (uint8 i = 0; i < 2; i++) {
            result[i] = data[i];
        }
        return result;
    }

    function shift(uint256 x, int256 _shift) internal pure returns (uint256) {
        if (_shift > 0) {
            return x << abs(_shift);
        } else if (_shift < 0) {
            return x >> abs(_shift);
        }

        return x;
    }

    function bitwiseOr(uint256 x, uint256 y) internal pure returns (uint256) {
        return x | y;
    }

    function bitwiseAnd(uint256 x, uint256 y) internal pure returns (uint256) {
        return x & y;
    }

    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a >= b ? a : b;
    }

    function max(int256 a, int256 b) internal pure returns (int256) {
        return a >= b ? a : b;
    }

    function min(int256 a, int256 b) internal pure returns (int256) {
        return a < b ? a : b;
    }

    function abs(int256 value) internal pure returns (uint256) {
        return value >= 0 ? toUint256(value) : toUint256(neg256(value));
    }

    function neg256(int256 a) internal pure returns (int256) {
        require(a > -2**255, "FairtradeMath: inversion overflow");
        return -a;
    }

    function neg256(uint256 a) internal pure returns (int256) {
        return -toInt256(a);
    }

    function formatX1e18ToX96(uint256 valueX10_18) internal pure returns (uint256) {
        return FullMath.mulDiv(valueX10_18, Constant.IQ96, 1 ether);
    }

    function mulRatio(uint256 value, uint24 ratio) internal pure returns (uint256) {
        return FullMath.mulDiv(value, ratio, 1e6);
    }

    /// @param denominator cannot be 0 and is checked in FullMath.mulDiv()
    function mulDiv(
        int256 a,
        int256 b,
        uint256 denominator
    ) internal pure returns (int256 result) {
        uint256 unsignedA = a < 0 ? uint256(neg256(a)) : uint256(a);
        uint256 unsignedB = b < 0 ? uint256(neg256(b)) : uint256(b);
        bool negative = ((a < 0 && b > 0) || (a > 0 && b < 0)) ? true : false;

        uint256 unsignedResult = FullMath.mulDiv(unsignedA, unsignedB, denominator);

        result = negative ? neg256(unsignedResult) : toInt256(unsignedResult);

        return result;
    }

    /**
     * @dev Returns the downcasted uint32 from uint256, reverting on
     * overflow (when the input is greater than largest uint32).
     *
     * Counterpart to Solidity's `uint32` operator.
     *
     * Requirements:
     *
     * - input must fit into 32 bits
     */
    function toUint32(uint256 value) internal pure returns (uint32 returnValue) {
        require(((returnValue = uint32(value)) == value), "SafeCast: value doesn't fit in 32 bits");
    }

    /**
     * @dev Converts a signed int256 into an unsigned uint256.
     *
     * Requirements:
     *
     * - input must be greater than or equal to 0.
     */
    function toUint256(int256 value) internal pure returns (uint256) {
        require(value >= 0, "SafeCast: value must be positive");
        return uint256(value);
    }

    /**
     * @dev Converts an unsigned uint256 into a signed int256.
     *
     * Requirements:
     *
     * - input must be less than or equal to maxInt256.
     */
    function toInt256(uint256 value) internal pure returns (int256) {
        require(value <= uint256(type(int256).max), "SafeCast: value doesn't fit in an int256");
        return int256(value);
    }
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.0;

/// @title Contains 512-bit math functions
/// @notice Facilitates multiplication and division that can have overflow of an intermediate value without any loss of precision
/// @dev Handles "phantom overflow" i.e., allows multiplication and division where an intermediate value overflows 256 bits
library FullMath {
    /// @notice Calculates floor(a×b÷denominator) with full precision. Throws if result overflows a uint256 or denominator == 0
    /// @param a The multiplicand
    /// @param b The multiplier
    /// @param denominator The divisor
    /// @return result The 256-bit result
    /// @dev Credit to Remco Bloemen under MIT license https://xn--2-umb.com/21/muldiv
    function mulDiv(
        uint256 a,
        uint256 b,
        uint256 denominator
    ) internal pure returns (uint256 result) {
        unchecked {
            // 512-bit multiply [prod1 prod0] = a * b
            // Compute the product mod 2**256 and mod 2**256 - 1
            // then use the Chinese Remainder Theorem to reconstruct
            // the 512 bit result. The result is stored in two 256
            // variables such that product = prod1 * 2**256 + prod0
            uint256 prod0; // Least significant 256 bits of the product
            uint256 prod1; // Most significant 256 bits of the product
            assembly {
                let mm := mulmod(a, b, not(0))
                prod0 := mul(a, b)
                prod1 := sub(sub(mm, prod0), lt(mm, prod0))
            }

            // Handle non-overflow cases, 256 by 256 division
            if (prod1 == 0) {
                require(denominator > 0, "denominator must be greater than 0");
                assembly {
                    result := div(prod0, denominator)
                }
                return result;
            }

            // Make sure the result is less than 2**256.
            // Also prevents denominator == 0
            require(denominator > prod1);

            ///////////////////////////////////////////////
            // 512 by 256 division.
            ///////////////////////////////////////////////

            // Make division exact by subtracting the remainder from [prod1 prod0]
            // Compute remainder using mulmod
            uint256 remainder;
            assembly {
                remainder := mulmod(a, b, denominator)
            }
            // Subtract 256 bit number from 512 bit number
            assembly {
                prod1 := sub(prod1, gt(remainder, prod0))
                prod0 := sub(prod0, remainder)
            }

            // Factor powers of two out of denominator
            // Compute largest power of two divisor of denominator.
            // Always >= 1.
            uint256 twos = (0 - denominator) & denominator;
            // Divide denominator by power of two
            assembly {
                denominator := div(denominator, twos)
            }

            // Divide [prod1 prod0] by the factors of two
            assembly {
                prod0 := div(prod0, twos)
            }
            // Shift in bits from prod1 into prod0. For this we need
            // to flip `twos` such that it is 2**256 / twos.
            // If twos is zero, then it becomes one
            assembly {
                twos := add(div(sub(0, twos), twos), 1)
            }
            prod0 |= prod1 * twos;

            // Invert denominator mod 2**256
            // Now that denominator is an odd number, it has an inverse
            // modulo 2**256 such that denominator * inv = 1 mod 2**256.
            // Compute the inverse by starting with a seed that is correct
            // correct for four bits. That is, denominator * inv = 1 mod 2**4
            uint256 inv = (3 * denominator) ^ 2;
            // Now use Newton-Raphson iteration to improve the precision.
            // Thanks to Hensel's lifting lemma, this also works in modular
            // arithmetic, doubling the correct bits in each step.
            inv *= 2 - denominator * inv; // inverse mod 2**8
            inv *= 2 - denominator * inv; // inverse mod 2**16
            inv *= 2 - denominator * inv; // inverse mod 2**32
            inv *= 2 - denominator * inv; // inverse mod 2**64
            inv *= 2 - denominator * inv; // inverse mod 2**128
            inv *= 2 - denominator * inv; // inverse mod 2**256

            // Because the division is now exact we can divide by multiplying
            // with the modular inverse of denominator. This will give us the
            // correct result modulo 2**256. Since the precoditions guarantee
            // that the outcome is less than 2**256, this is the final result.
            // We don't need to compute the high bits of the result and prod1
            // is no longer required.
            result = prod0 * inv;
            return result;
        }
    }

    /// @notice Calculates ceil(a×b÷denominator) with full precision. Throws if result overflows a uint256 or denominator == 0
    /// @param a The multiplicand
    /// @param b The multiplier
    /// @param denominator The divisor
    /// @return result The 256-bit result
    function mulDivRoundingUp(
        uint256 a,
        uint256 b,
        uint256 denominator
    ) internal pure returns (uint256 result) {
        unchecked {
            result = mulDiv(a, b, denominator);
            if (mulmod(a, b, denominator) > 0) {
                require(result < type(uint256).max);
                result++;
            }
        }
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;

import { FairtradeMath } from "./FairtradeMath.sol";
import { Account } from "./Account.sol";
import { Constant } from "./Constant.sol";

library Funding {
    using FairtradeMath for int256;
    using FairtradeMath for uint256;

    //
    // STRUCT
    //

    /// @dev tw: time-weighted
    /// @param twPremium overflow inspection (as twPremium > twPremiumWithLiquidity):
    //         max = 2 ^ (255 - 96) = 2 ^ 159 = 7.307508187E47
    //         assume premium = 10000, time = 10 year = 60 * 60 * 24 * 365 * 10 -> twPremium = 3.1536E12
    struct Growth {
        int256 twPremium;
        int256 twPremiumWithLiquidity;
    }

    //
    // CONSTANT
    //

    /// @dev block-based funding is calculated as: premium * timeFraction / 1 day, for 1 day as the default period
    int256 internal constant _DEFAULT_FUNDING_PERIOD = 1 days;

    //
    // INTERNAL PURE
    //

    function calcPendingFundingPayment(
        int256 baseBalance,
        int256 twPremiumGrowthGlobal,
        Growth memory fundingGrowthGlobal,
        int256 liquidityFundingPayment
    ) internal pure returns (int256) {
        int256 positionFundingPayment = FairtradeMath.mulDiv(
            baseBalance,
            (fundingGrowthGlobal.twPremium - twPremiumGrowthGlobal),
            Constant.IQ96
        );

        int256 pendingFundingPayment = (liquidityFundingPayment + positionFundingPayment) / _DEFAULT_FUNDING_PERIOD;

        // make RoundingUp to avoid bed debt
        // if pendingFundingPayment > 0: long pay 1wei more, short got 1wei less
        // if pendingFundingPayment < 0: long got 1wei less, short pay 1wei more
        if (pendingFundingPayment != 0) {
            pendingFundingPayment++;
        }

        return pendingFundingPayment;
    }

    /// @return liquidityFundingPayment the funding payment of an LP order
    function calcLiquidityFundingPayment(Account.LPInfo memory order, Funding.Growth memory fundingGrowthGlobal)
        internal
        pure
        returns (int256)
    {
        if (order.liquidity == 0) {
            return 0;
        }

        int256 fundingPaymentLP = order.liquidity.toInt256() *
            (fundingGrowthGlobal.twPremiumWithLiquidity - order.lastTwPremiumWithLiquidityGrowth);
        return fundingPaymentLP / Constant.IQ96_SIGNED;
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.0;

import { Account } from "../lib/Account.sol";

/// @notice For future upgrades, do not change LiquidityProviderStorageV1. Create a new
/// contract which implements LiquidityProviderStorageV1 and following the naming convention
/// LiquidityProviderStorageVX.
abstract contract LiquidityProviderStorageV1 {
    address internal _marketTaker;
    address internal _ammFactory;

    // first key: trader, second key: base token
    mapping(address => mapping(address => Account.LPInfo)) internal _openOrderMap;

    // key: base token
    // value: the global accumulator of **quote fee transformed from base fee** of each amm
    mapping(address => uint256) internal _feeGrowthGlobalX128Map;

    // TODO: ******remove below mapping before deploy to prod.********
    // address: baseToken, value: fee/_totalLiquidity
    mapping(address => uint256[]) public _totalLPCalcFeeRatioMap;

    uint256 internal _feeGrowthPerLiquidityUnitIndex;

    // address: baseToken
    // uint256: the global accumulator of trade fee for liquidity unit
    mapping(address => mapping(uint256 => uint256)) internal _feeGrowthPerLiquidityUnitMap;
    // address: baseToken
    // uint256: index
    mapping(address => uint256) internal _feeGrowthPerLiquidityUnitIndexMap;
}