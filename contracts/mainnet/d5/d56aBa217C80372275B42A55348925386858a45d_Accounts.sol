// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (utils/math/SafeMath.sol)

pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is generally not needed starting with Solidity 0.8, since the compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

import { Decimal } from "../libraries/LibDecimal.sol";
import { ConstantsStorage } from "./ConstantsStorage.sol";
import { IConstantsEvents } from "./IConstantsEvents.sol";

library ConstantsInternal {
    using ConstantsStorage for ConstantsStorage.Layout;
    using Decimal for Decimal.D256;

    uint256 private constant PERCENT_BASE = 1e18;
    uint256 private constant PRECISION = 1e18;

    /* ========== VIEWS ========== */

    function getPrecision() internal pure returns (uint256) {
        return PRECISION;
    }

    function getPercentBase() internal pure returns (uint256) {
        return PERCENT_BASE;
    }

    function getCollateral() internal view returns (address) {
        return ConstantsStorage.layout().collateral;
    }

    function getLiquidationFee() internal view returns (Decimal.D256 memory) {
        return Decimal.ratio(ConstantsStorage.layout().liquidationFee, PERCENT_BASE);
    }

    function getProtocolLiquidationShare() internal view returns (Decimal.D256 memory) {
        return Decimal.ratio(ConstantsStorage.layout().protocolLiquidationShare, PERCENT_BASE);
    }

    function getCVA() internal view returns (Decimal.D256 memory) {
        return Decimal.ratio(ConstantsStorage.layout().cva, PERCENT_BASE);
    }

    function getRequestTimeout() internal view returns (uint256) {
        return ConstantsStorage.layout().requestTimeout;
    }

    function getMaxOpenPositionsCross() internal view returns (uint256) {
        return ConstantsStorage.layout().maxOpenPositionsCross;
    }

    /* ========== SETTERS ========== */

    function setCollateral(address collateral) internal {
        ConstantsStorage.layout().collateral = collateral;
    }

    function setLiquidationFee(uint256 liquidationFee) internal {
        ConstantsStorage.layout().liquidationFee = liquidationFee;
    }

    function setProtocolLiquidationShare(uint256 protocolLiquidationShare) internal {
        ConstantsStorage.layout().protocolLiquidationShare = protocolLiquidationShare;
    }

    function setCVA(uint256 cva) internal {
        ConstantsStorage.layout().cva = cva;
    }

    function setRequestTimeout(uint256 requestTimeout) internal {
        ConstantsStorage.layout().requestTimeout = requestTimeout;
    }

    function setMaxOpenPositionsCross(uint256 maxOpenPositionsCross) internal {
        ConstantsStorage.layout().maxOpenPositionsCross = maxOpenPositionsCross;
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

library ConstantsStorage {
    bytes32 internal constant STORAGE_SLOT = keccak256("diamond.standard.constants.storage");

    struct Layout {
        address collateral;
        uint256 liquidationFee;
        uint256 protocolLiquidationShare;
        uint256 cva;
        uint256 requestTimeout;
        uint256 maxOpenPositionsCross;
    }

    function layout() internal pure returns (Layout storage l) {
        bytes32 slot = STORAGE_SLOT;
        assembly {
            l.slot := slot
        }
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

interface IConstantsEvents {
    event SetCollateral(address oldAddress, address newAddress);
    event SetLiquidationFee(uint256 oldFee, uint256 newFee);
    event SetProtocolLiquidationShare(uint256 oldShare, uint256 newShare);
    event SetCVA(uint256 oldCVA, uint256 newCVA);
    event SetRequestTimeout(uint256 oldTimeout, uint256 newTimeout);
    event SetMaxOpenPositionsCross(uint256 oldMax, uint256 newMax);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import "@openzeppelin/contracts/utils/math/SafeMath.sol";

/**
 * @title Decimal
 * @author dYdX
 *
 * Library that defines a fixed-point number with 18 decimal places.
 */
library Decimal {
    using SafeMath for uint256;

    // ============ Constants ============

    uint256 constant BASE = 10 ** 18;

    // ============ Structs ============

    struct D256 {
        uint256 value;
    }

    // ============ Static Functions ============

    function zero() internal pure returns (D256 memory) {
        return D256({ value: 0 });
    }

    function one() internal pure returns (D256 memory) {
        return D256({ value: BASE });
    }

    function from(uint256 a) internal pure returns (D256 memory) {
        return D256({ value: a.mul(BASE) });
    }

    function ratio(uint256 a, uint256 b) internal pure returns (D256 memory) {
        return D256({ value: getPartial(a, BASE, b) });
    }

    // ============ Self Functions ============

    function add(D256 memory self, uint256 b) internal pure returns (D256 memory) {
        return D256({ value: self.value.add(b.mul(BASE)) });
    }

    function sub(D256 memory self, uint256 b) internal pure returns (D256 memory) {
        return D256({ value: self.value.sub(b.mul(BASE)) });
    }

    function sub(D256 memory self, uint256 b, string memory reason) internal pure returns (D256 memory) {
        return D256({ value: self.value.sub(b.mul(BASE), reason) });
    }

    function mul(D256 memory self, uint256 b) internal pure returns (D256 memory) {
        return D256({ value: self.value.mul(b) });
    }

    function div(D256 memory self, uint256 b) internal pure returns (D256 memory) {
        return D256({ value: self.value.div(b) });
    }

    function pow(D256 memory self, uint256 b) internal pure returns (D256 memory) {
        if (b == 0) {
            return one();
        }

        D256 memory temp = D256({ value: self.value });
        for (uint256 i = 1; i < b; ++i) {
            temp = mul(temp, self);
        }

        return temp;
    }

    function add(D256 memory self, D256 memory b) internal pure returns (D256 memory) {
        return D256({ value: self.value.add(b.value) });
    }

    function sub(D256 memory self, D256 memory b) internal pure returns (D256 memory) {
        return D256({ value: self.value.sub(b.value) });
    }

    function sub(D256 memory self, D256 memory b, string memory reason) internal pure returns (D256 memory) {
        return D256({ value: self.value.sub(b.value, reason) });
    }

    function mul(D256 memory self, D256 memory b) internal pure returns (D256 memory) {
        return D256({ value: getPartial(self.value, b.value, BASE) });
    }

    function div(D256 memory self, D256 memory b) internal pure returns (D256 memory) {
        return D256({ value: getPartial(self.value, BASE, b.value) });
    }

    function equals(D256 memory self, D256 memory b) internal pure returns (bool) {
        return self.value == b.value;
    }

    function greaterThan(D256 memory self, D256 memory b) internal pure returns (bool) {
        return compareTo(self, b) == 2;
    }

    function lessThan(D256 memory self, D256 memory b) internal pure returns (bool) {
        return compareTo(self, b) == 0;
    }

    function greaterThanOrEqualTo(D256 memory self, D256 memory b) internal pure returns (bool) {
        return compareTo(self, b) > 0;
    }

    function lessThanOrEqualTo(D256 memory self, D256 memory b) internal pure returns (bool) {
        return compareTo(self, b) < 2;
    }

    function isZero(D256 memory self) internal pure returns (bool) {
        return self.value == 0;
    }

    function asUint256(D256 memory self) internal pure returns (uint256) {
        return self.value.div(BASE);
    }

    // ============ Core Methods ============

    function getPartial(uint256 target, uint256 numerator, uint256 denominator) private pure returns (uint256) {
        return target.mul(numerator).div(denominator);
    }

    function compareTo(D256 memory a, D256 memory b) private pure returns (uint256) {
        if (a.value == b.value) {
            return 1;
        }
        return a.value > b.value ? 2 : 0;
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

enum MarketType {
    FOREX,
    METALS,
    ENERGIES,
    INDICES,
    STOCKS,
    COMMODITIES,
    BONDS,
    ETFS,
    CRYPTO
}

enum Side {
    BUY,
    SELL
}

enum HedgerMode {
    SINGLE,
    HYBRID,
    AUTO
}

enum OrderType {
    LIMIT,
    MARKET
}

enum PositionType {
    ISOLATED,
    CROSS
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

import { PositionType, OrderType, HedgerMode, Side } from "../libraries/LibEnums.sol";

enum RequestForQuoteState {
    NEW,
    CANCELED,
    ACCEPTED
}

enum PositionState {
    OPEN,
    MARKET_CLOSE_REQUESTED,
    LIMIT_CLOSE_REQUESTED,
    LIMIT_CLOSE_ACTIVE,
    CLOSED,
    LIQUIDATED
    // TODO: add cancel limit close
}

struct RequestForQuote {
    uint256 creationTimestamp;
    uint256 mutableTimestamp;
    uint256 rfqId;
    RequestForQuoteState state;
    PositionType positionType;
    OrderType orderType;
    address partyA;
    address partyB;
    HedgerMode hedgerMode;
    uint256 marketId;
    Side side;
    uint256 notionalUsd;
    uint256 lockedMarginA;
    uint256 protocolFee;
    uint256 liquidationFee;
    uint256 cva;
    uint256 minExpectedUnits;
    uint256 maxExpectedUnits;
    address affiliate;
}

struct Position {
    uint256 creationTimestamp;
    uint256 mutableTimestamp;
    uint256 positionId;
    bytes16 uuid;
    PositionState state;
    PositionType positionType;
    uint256 marketId;
    address partyA;
    address partyB;
    Side side;
    uint256 lockedMarginA;
    uint256 lockedMarginB;
    uint256 protocolFeePaid;
    uint256 liquidationFee;
    uint256 cva;
    uint256 currentBalanceUnits;
    uint256 initialNotionalUsd;
    address affiliate;
}

library MasterStorage {
    bytes32 internal constant STORAGE_SLOT = keccak256("diamond.standard.master.agreement.storage");

    struct Layout {
        // Balances
        mapping(address => uint256) accountBalances;
        mapping(address => uint256) marginBalances;
        mapping(address => uint256) crossLockedMargin;
        mapping(address => uint256) crossLockedMarginReserved;
        // RequestForQuotes
        mapping(uint256 => RequestForQuote) requestForQuotesMap;
        uint256 requestForQuotesLength;
        mapping(address => uint256) crossRequestForQuotesLength;
        // Positions
        mapping(uint256 => Position) allPositionsMap;
        uint256 allPositionsLength;
        mapping(address => uint256) openPositionsIsolatedLength;
        mapping(address => uint256) openPositionsCrossLength;
    }

    function layout() internal pure returns (Layout storage l) {
        bytes32 slot = STORAGE_SLOT;
        assembly {
            l.slot := slot
        }
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

import { AccountsInternal } from "./AccountsInternal.sol";
import { ReentrancyGuard } from "../../security/ReentrancyGuard.sol";
import { IAccountsEvents } from "./IAccountsEvents.sol";

contract Accounts is ReentrancyGuard, IAccountsEvents {
    /* ========== VIEWS ========== */

    function getAccountBalance(address party) external view returns (uint256) {
        return AccountsInternal.getAccountBalance(party);
    }

    function getMarginBalance(address party) external view returns (uint256) {
        return AccountsInternal.getMarginBalance(party);
    }

    function getLockedMarginIsolated(address party, uint256 positionId) external view returns (uint256) {
        return AccountsInternal.getLockedMarginIsolated(party, positionId);
    }

    function getLockedMarginCross(address party) external view returns (uint256) {
        return AccountsInternal.getLockedMarginCross(party);
    }

    function getLockedMarginReserved(address party) external view returns (uint256) {
        return AccountsInternal.getLockedMarginReserved(party);
    }

    /* ========== WRITES ========== */

    function deposit(uint256 amount) external nonReentrant {
        AccountsInternal.deposit(msg.sender, amount);
        emit Deposit(msg.sender, amount);
    }

    function withdraw(uint256 amount) external nonReentrant {
        AccountsInternal.withdraw(msg.sender, amount);
        emit Withdraw(msg.sender, amount);
    }

    function allocate(uint256 amount) external {
        AccountsInternal.allocate(msg.sender, amount);
        emit Allocate(msg.sender, amount);
    }

    function deallocate(uint256 amount) external {
        AccountsInternal.deallocate(msg.sender, amount);
        emit Deallocate(msg.sender, amount);
    }

    function depositAndAllocate(uint256 amount) external nonReentrant {
        AccountsInternal.deposit(msg.sender, amount);
        AccountsInternal.allocate(msg.sender, amount);

        emit Deposit(msg.sender, amount);
        emit Allocate(msg.sender, amount);
    }

    function deallocateAndWithdraw(uint256 amount) external nonReentrant {
        AccountsInternal.deallocate(msg.sender, amount);
        AccountsInternal.withdraw(msg.sender, amount);

        emit Deallocate(msg.sender, amount);
        emit Withdraw(msg.sender, amount);
    }

    function addFreeMarginIsolated(uint256 amount, uint256 positionId) external {
        AccountsInternal.addFreeMarginIsolated(msg.sender, amount, positionId);
        emit AddFreeMarginIsolated(msg.sender, amount, positionId);
    }

    function addFreeMarginCross(uint256 amount) external {
        AccountsInternal.addFreeMarginCross(msg.sender, amount);
        emit AddFreeMarginCross(msg.sender, amount);
    }

    function removeFreeMarginCross() external {
        uint256 removedAmount = AccountsInternal.removeFreeMarginCross(msg.sender);
        emit RemoveFreeMarginCross(msg.sender, removedAmount);
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { MasterStorage, Position, PositionState } from "../MasterStorage.sol";
import { ConstantsInternal } from "../../constants/ConstantsInternal.sol";

library AccountsInternal {
    using MasterStorage for MasterStorage.Layout;

    /* ========== VIEWS ========== */

    function getAccountBalance(address party) internal view returns (uint256) {
        return MasterStorage.layout().accountBalances[party];
    }

    function getMarginBalance(address party) internal view returns (uint256) {
        return MasterStorage.layout().marginBalances[party];
    }

    function getLockedMarginIsolated(address party, uint256 positionId) internal view returns (uint256) {
        MasterStorage.Layout storage s = MasterStorage.layout();
        Position storage position = s.allPositionsMap[positionId];

        require(position.partyA == party || position.partyB == party, "Invalid party");
        return position.partyA == party ? position.lockedMarginA : position.lockedMarginB;
    }

    function getLockedMarginCross(address party) internal view returns (uint256) {
        return MasterStorage.layout().crossLockedMargin[party];
    }

    function getLockedMarginReserved(address party) internal view returns (uint256) {
        return MasterStorage.layout().crossLockedMarginReserved[party];
    }

    /* ========== WRITES ========== */

    function deposit(address party, uint256 amount) internal {
        MasterStorage.Layout storage s = MasterStorage.layout();

        IERC20 collateral = IERC20(ConstantsInternal.getCollateral());
        require(collateral.balanceOf(party) >= amount, "Insufficient collateral balance");

        bool success = collateral.transferFrom(party, address(this), amount);
        require(success, "Failed to deposit collateral");
        s.accountBalances[party] += amount;
    }

    function withdraw(address party, uint256 amount) internal {
        MasterStorage.Layout storage s = MasterStorage.layout();

        require(s.accountBalances[party] >= amount, "Insufficient account balance");
        s.accountBalances[party] -= amount;
        bool success = IERC20(ConstantsInternal.getCollateral()).transfer(party, amount);
        require(success, "Failed to withdraw collateral");
    }

    function withdrawRevenue(address delegate, uint256 amount) internal {
        MasterStorage.Layout storage s = MasterStorage.layout();

        require(s.accountBalances[address(this)] >= amount, "Insufficient account balance");
        s.accountBalances[address(this)] -= amount;
        bool success = IERC20(ConstantsInternal.getCollateral()).transfer(delegate, amount);
        require(success, "Failed to withdraw collateral");
    }

    function allocate(address party, uint256 amount) internal {
        MasterStorage.Layout storage s = MasterStorage.layout();

        require(s.accountBalances[party] >= amount, "Insufficient account balance");
        s.accountBalances[party] -= amount;
        s.marginBalances[party] += amount;
    }

    function deallocate(address party, uint256 amount) internal {
        MasterStorage.Layout storage s = MasterStorage.layout();

        require(s.marginBalances[party] >= amount, "Insufficient margin balance");
        s.marginBalances[party] -= amount;
        s.accountBalances[party] += amount;
    }

    function addFreeMarginIsolated(address party, uint256 amount, uint256 positionId) internal {
        MasterStorage.Layout storage s = MasterStorage.layout();
        Position storage position = s.allPositionsMap[positionId];

        require(s.marginBalances[party] >= amount, "Insufficient margin balance");
        s.marginBalances[party] -= amount;

        require(position.partyA == party || position.partyB == party, "Invalid party");
        require(
            position.state != PositionState.CLOSED && position.state != PositionState.LIQUIDATED,
            "Invalid position state"
        );

        if (position.partyA == party) {
            position.lockedMarginA += amount;
        } else {
            position.lockedMarginB += amount;
        }
    }

    function addFreeMarginCross(address party, uint256 amount) internal {
        MasterStorage.Layout storage s = MasterStorage.layout();

        require(s.marginBalances[party] >= amount, "Insufficient margin balance");
        s.marginBalances[party] -= amount;
        s.crossLockedMargin[party] += amount;
    }

    function removeFreeMarginCross(address party) internal returns (uint256 removedAmount) {
        MasterStorage.Layout storage s = MasterStorage.layout();

        require(s.openPositionsCrossLength[party] == 0, "Removal denied");
        require(s.crossLockedMargin[party] > 0, "No locked margin");

        uint256 amount = s.crossLockedMargin[party];
        s.crossLockedMargin[party] = 0;
        s.marginBalances[party] += amount;

        return amount;
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

interface IAccountsEvents {
    event Deposit(address indexed party, uint256 amount);
    event Withdraw(address indexed party, uint256 amount);
    event Allocate(address indexed party, uint256 amount);
    event Deallocate(address indexed party, uint256 amount);
    event AddFreeMarginIsolated(address indexed party, uint256 amount, uint256 indexed positionId);
    event AddFreeMarginCross(address indexed party, uint256 amount);
    event RemoveFreeMarginCross(address indexed party, uint256 amount);
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

import { SecurityStorage } from "./SecurityStorage.sol";

abstract contract ReentrancyGuard {
    using SecurityStorage for SecurityStorage.Layout;

    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    modifier nonReentrant() {
        SecurityStorage.Layout storage s = SecurityStorage.layout();

        require(s.reentrantStatus != _ENTERED, "ReentrancyGuard: reentrant call");
        s.reentrantStatus = _ENTERED;
        _;
        s.reentrantStatus = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.16;

library SecurityStorage {
    bytes32 internal constant STORAGE_SLOT = keccak256("diamond.standard.security.storage");

    struct Layout {
        uint256 reentrantStatus;
    }

    function layout() internal pure returns (Layout storage l) {
        bytes32 slot = STORAGE_SLOT;
        assembly {
            l.slot := slot
        }
    }
}